plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
    id 'com.google.gms.google-services'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "ru.geekbrains.filmserach"
        minSdk 29
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        testOptions {
            unitTests {
                includeAndroidResources = true
            }
        }
    }

    Properties properties = new Properties()
    properties.load(
            project.file("signing.properties").newDataInputStream()
    )

    signingConfigs {
        release {
            storeFile file("keystore.jks")
            storePassword properties.getProperty("STORE_PASSWORD")
            keyAlias properties.getProperty("KEY_ALIAS")
            keyPassword properties.getProperty("KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            versionNameSuffix '-release'
            buildConfigField "String", "TYPE", '"RELEASE"'
        }

        debug {
            versionNameSuffix '-debug'
            buildConfigField "String", "TYPE", '"DEBUG"'
        }
    }

    flavorDimensions 'version'

    productFlavors {
        free {
            dimension 'version'
            versionNameSuffix '.free'
        }
    }

    buildTypes.each {
        properties.load(
                project.rootProject.file("api.properties").newDataInputStream()
        )

        def token = properties.getProperty("token", "")
        it.buildConfigField "String", "TOKEN", token
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        viewBinding true
    }

    sourceSets {
        androidTest {
            java.srcDirs += "src/commonTest/java"
        }
        test {
            java.srcDirs += "src/commonTest/java"
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.room:room-common:2.4.2'
    implementation 'androidx.room:room-ktx:2.4.2'

    implementation 'com.google.android.gms:play-services-maps:18.0.2'
    implementation 'com.google.firebase:firebase-messaging-ktx:23.0.3'
    implementation 'androidx.test:core-ktx:1.5.0'
    implementation 'androidx.test.ext:junit-ktx:1.1.5'

    // Navigation (bottom)
    def navigation_version = "2.3.5"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    // Lifecycle
    def lifecycle_version = "2.3.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    // DI: Koin
    def koin_version = "3.1.2"
    implementation "io.insert-koin:koin-core:$koin_version"
    implementation "io.insert-koin:koin-android:$koin_version"

    implementation 'com.squareup.okhttp3:okhttp:3.14.9'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.2'
    implementation "com.google.code.gson:gson:2.8.9"

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Room
    def room_version = "2.4.3"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation 'com.google.android.gms:play-services-maps:18.1.0'

    // vectors graphic
    implementation "io.coil-kt:coil:1.3.2"

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test.ext:junit:1.1.5'

    testImplementation "org.mockito:mockito-core:5.5.0"

    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testImplementation('com.nhaarman:mockito-kotlin:1.6.0') {
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'org.mockito'
    }

    testImplementation 'androidx.arch.core:core-testing:2.1.0'

    // Robolectric
    testImplementation 'org.robolectric:robolectric:4.5.1'
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation 'androidx.test:runner:1.5.2'
    testImplementation 'androidx.test.ext:truth:1.5.0'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    debugImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'

    androidTestImplementation 'androidx.navigation:navigation-testing:2.6.0'

    // UI Automator
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    debugImplementation 'androidx.fragment:fragment-testing:1.3.3'

    testImplementation 'org.hamcrest:hamcrest-all:1.3'
}
